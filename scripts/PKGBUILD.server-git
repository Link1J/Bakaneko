# Maintainer: Jared Irwin <jrairwin@sympatico.ca>
pkgname=bakaneko-server-git
pkgver=0.1.1
pkgrel=1
pkgdesc="Server for Bakaneko"
arch=('x86_64')
url="https://github.com/Link1J/Bakaneko"
license=('GPL3')
depends=('boost' 'protobuf' 'openssl' 'dbus')
makedepends=('cmake' 'git' 'extra-cmake-modules')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
backup=('etc/bakaneko-server.ini')
source=("${pkgname}::git+https://github.com/Link1J/Bakaneko.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S $pkgname \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBAKANEKO_BUILD_CLIENT=OFF
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir/" install
  install -Dm600 "$pkgname/extras/bakaneko-server.ini" "$pkgdir/etc/bakaneko-server.ini"
  install -Dm644 "$pkgname/extras/bakaneko-server.service" "$pkgdir/usr/lib/systemd/system/bakaneko-server.service"
}
