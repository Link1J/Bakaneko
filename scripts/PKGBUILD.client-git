# Maintainer: Jared Irwin <jrairwin@sympatico.ca>
pkgname=bakaneko-git
pkgver=0.1.1
pkgrel=1
pkgdesc="Bakaneko Client"
arch=('x86_64')
url="https://github.com/Link1J/Bakaneko"
license=('GPL3')
depends=('boost' 'protobuf' 'openssl' 'qqc2-desktop-style' 'kiconthemes' 'kcrash' 'kdeclarative'
  'ki18n' 'kconfig' 'kcoreaddons' 'qt5-base' 'qt5-declarative' 'qt5-svg' 'qt5-quickcontrols2'
  'libssh')
makedepends=('cmake' 'extra-cmake-modules' 'git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=("${pkgname}::git+https://github.com/Link1J/Bakaneko.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S ${pkgname} \
    -Wno-dev \
    -DCMAKE_BUILD_TYPE='None' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DBAKANEKO_DONT_BUILD_SERVER=ON
  make -C build
}

package() {
  make -C build DESTDIR="$pkgdir/" install
}
